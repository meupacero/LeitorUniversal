plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdk versao.compileSdk

    defaultConfig {
        minSdk versao.minSdk
        targetSdk versao.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${archivesBaseName}_${versao.versionName}.aar"
        }
    }
    viewBinding.enabled = true
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'leitor.universal'
                artifactId = 'scanner'
                version = versao.versionName
            }
        }
    }
}

dependencies {
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
}